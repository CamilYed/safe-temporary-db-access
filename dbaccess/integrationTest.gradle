sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
}

if (project.plugins.findPlugin('idea')) {
    idea {
        module {
            testSourceDirs += sourceSets.integrationTest.java.srcDirs
            testResourceDirs += sourceSets.integrationTest.resources.srcDirs
            scopes.TEST.plus += [configurations.integrationTestCompile]
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

tasks.register('integrationTest', Test) {
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
    useJUnitPlatform()
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test
}

check.dependsOn integrationTest
